apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}


tasks.withType(Javadoc).all {
    enabled = false
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = "com.qifan.rotateLabelView"
                artifactId = "rotateLabelView"
                version = "1.0.0"
                artifact androidSourcesJar
                artifact androidJavadocsJar
                pom {
                    name = "RotateLabelView"
                    description = "This is library to help you to add a sticky rotation label into your view."
                    url = "https://github.com/underwindfall/RotateLabelView"
                    developers {
                        developer {
                            id = "underwindfall"
                            name = "Qifan YANG"
                        }
                    }
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        connection = "git@github.com:underwindfall/RotateLabelView.git"
                        developerConnection = "git@github.com:underwindfall/RotateLabelView.git"
                        url = "https://github.com/underwindfall/RotateLabelView"
                    }
                }
                from components.release
            }
        }
    }
}

bintray {
    user = System.getenv("bintrayUser")
    key = System.getenv("bintrayApiKey")
    pkg {
        repo = "maven"
        name = "rotateLabelView"
        websiteUrl = "https://github.com/underwindfall/RotateLabelView"
        vcsUrl = "git@github.com:underwindfall/RotateLabelView.git"
        licenses = ['Apache-2.0']
        publish = true
        version {
            name = "1.0.0"
        }
        publications = ['release']
    }
}